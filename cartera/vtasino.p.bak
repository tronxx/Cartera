* Programa que genera el reporte del informe de ventas brutas por zona
  de todo lo facturado si y no Ventas a Credito
  DRBR 13-Mar-1993
  ---------------------------------------------------------------------
*

#dMDS10 %,10.2f~; #dMDSF %6.2f~; #dMDS14 %,14.2f~; #dMIS2 %02d~;


f0; M
	version_z		s30,
	nemp_z          s80,
	direc_z         s80,
	modul_z         s30,
	modul2_z        s10,
        maxcol_z		i,
        maxrow_z		i,
        renst_z			i,
        sff_z			s2,
	impre_z			i,
	font_z			i,
	micar_z			s3,
	carac_z			i,
	cmds_z		{
	  cmd1_z		s7,
	  cmd2_z		s13,
	  cmd3_z		s30,
			}
	arch_z			s13,
	bandimp_z		s3,
	
	ter_z            	s2,
	zona_z         		s3,
	fech8_z           	D,
	fech9_z     {
	  dias_z        	s16,
	  hora_z         	s6,
	  resto_z        	s5,
	            }
	fecha_z           	D,
	fechb_z           	D,
	prmes_z			i,
	ulmes_z			i,
	fecini_z	{
	  feciniaa_z		i,
	  fecinimm_z		i,
	  fecinidd_z		i,
	  		}
	fecult_z	{
	  fecultaa_z		i,
	  fecultmm_z		i,
	  fecultdd_z		i,
	  		}
	fecsig_z	{
	  fecsigaa_z		i,
	  fecsigmm_z		i,
	  fecsigdd_z		i,
	  		}
        fecacuini_z	{
          fecacuiniaa_z	i,
          fecacuinimm_z	i,
          fecacuinidd_z	i,
        		}
        fecacufin_z	{
          fecacufinaa_z		i,
          fecacufinmm_z		i,
          fecacufindd_z		i,
        		}
	subzona_z		s4,
	fechc_z			D,
        ult_z			i,
        iii_z			i,

	ploc_z			s2,
        tipocar_z		s2,
        voc_z			s2,
	mibd_z			s80,
	milin_z			s100,
        prmegazona_z		s3,
        ulmegazona_z		s3,
        antmegazona_z		s3,
        megazona_z		s3,
        saltop_z		s2,

	vtaconsi_z		d,
	vtaconno_z		d,
	contadosi_z          	d,
	contadono_z          	d,
	vtacon_z          	d,
	vtabru_z          	d,
	vtabruz_z         	d,
	contadosiz_z         	d,
	contadonoz_z         	d,

	vtaconmzn_z          	d,
	vtabrumzn_z          	d,
	contadosimzn_z         	d,
	contadonomzn_z         	d,

	vtabrut_z         	d,
	vtacont_z         	d,
	vtaconz_z         	d,
	contadosit_z         	d,
	contadonot_z         	d,
	porc1_z           	d,
	porc2_z           	d,
	porc3_z           	d,
        fechd_z			D,
        feche_z			D,
        fechmai_z		D,
        fechmaf_z		D,
        fechaai_z		D,
        fechaaf_z		D,
        fechf_z			D,
        fechg_z			D,
        fechh_z			D,
        fechi_z			D,
        tasiva_z		s5,
        piva1_z			d,
        piva2_z			d,
	ind1_z            	i,
	ind2_z            	i,
	ii_z	            	i,
	termi_z           	i,
	esp_z             	i,
	exten_z           	i,
	tam0             	s0,;
mtam0&; 
* -- Manejo de Versiones ---
|  2011-Oct-21 Se modifica para que el dato que se imprime de la venta
|  de contado sea el importe del abono, no el total facturacion
|  -------------------------
|  19-Feb-2015 2.00 Se modifica para que tome en cuenta la division 
|  de las Megazonas
|___________________________________________________
*
q+,version_z,"V: 2.00 EB",;

tr; Ff3; FOdatos.ini~,r~; FGNodatos; Fknemp_z,l; &f0; tu1;
(impre_z)=(1); (arch_z)=($vtasino0.tex,12); 
Ff5; FO.impri.ini~,r~; FG|; | Fknemp_z,l; >sinemp_z,#,impre_z; . FC;
GPAN; GABRE_TABLA; GTOMA_PARAMETROS; GST;
(tasiva_z)=($XX,2); (ploc_z)=($L,1); (tipocar_z)=($T,1); (voc_z)=($V,1);

&a; &cfeche_z; qD,2,fecult_z,feche_z,;
(fecultdd_z)=(1); qD,1,fecult_z,feche_z,; (feche_z)=(feche_z)(86400)-;
qD,2,fecult_z,feche_z,; (fecultdd_z)=(1); qD,1,fecult_z,fechd_z,;

[fei; t@5,3; \Teclee la Fecha Inicial : \; t0Fin; (fecha_z)=(fechd_z);
ifecha_z,+10; !(fecha_z)0($-1)>fei; qD,2,fecini_z,fecha_z,;
[feu; t@6,3; \Teclee la Fecha Final   : \; t0fei; (fechb_z)=(feche_z);
ifechb_z,+10; !(fechb_z)0($-1)>feu; qD,2,fecult_z,fechb_z,;
[plc; t@7,3; \Acumulados en Precio `L`ista o `C`redito ? <L/C>:\; 
t0feu; iploc_z,1; !(ploc_z)5($L,1)(ploc_z)5($C,1)&>plc;
[tiv; t@8,3; \Ventas de Iva al (XX es total):\; t0plc; itasiva_z,2;
!(tasiva_z)0($XX,2)>|;
  (piva1_z)=(0); (piva2_z)=(100);
|
  >sdtasiva_z,#,piva1_z; (piva2_z)=(piva1_z);
.
(fecsig_z)=(fecini_z,#); GPRIMERO_DE_MES_ANTER;
qD,1,fecsig_z,fechf_z,; (fecsig_z)=(fecult_z,#); GULTIMO_DE_MES_ANTER; 
qD,1,fecsig_z,fechg_z,; (fecsig_z)=(fecini_z,#);
(fecsigaa_z)=(fecsigaa_z)(1)-; qD,1,fecsig_z,fechh_z,;
(fecsig_z)=(fecult_z,#); (fecsigaa_z)=(fecsigaa_z)(1)-; 
GULTIMO_DE_MES; qD,1,fecsig_z,fechi_z,;

[mai; t@9,3; \Inicial Mes Ant:\; t0tiv; (fechmai_z)=(fechf_z);
t@9,23; ifechmai_z,+10; !(fechmai_z)0($-1)>mai;
[maf; t@9,40; \Final   Mes Ant:\; t0mai; (fechmaf_z)=(fechg_z);
t@9,60; ifechmaf_z,+10; !(fechmaf_z)0($-1)>maf;
[aai; t@10,3; \Inicial Anu Ant:\; t0maf; (fechaai_z)=(fechh_z);
t@10,23; ifechaai_z,+10; !(fechaai_z)0($-1)>aai;
[aaf; t@10,40; \Final   Anu Ant:\; t0aai; (fechaaf_z)=(fechi_z);
t@10,60; ifechaaf_z,+10; !(fechaaf_z)0($-1)>aaf;

[prm; t@12,3; \Mega Zona Inicial:\; t0aaf; iprmegazona_z,2;
[ulm; t@12,40; \Mega Zona Final:\; t0prm; iulmegazona_z,2;
[spa; t@13,3; \Deseas Salto de Pagina x c/Megazona ? <S/N>:\; t0ulm;
isaltop_z,1; !(saltop_z)5($S,1)(saltop_z)5($N,1)&>spa;


GST; \Estan correctos los datos ? <S/N> : \; y|; | gfeu; .
GST; \Primero se Recorren las Tiendas para determinar el total ...\;
tS1,10,1,10,78; tc;
GGRABA_PARAMETROS;

f3; k1; P;
{ n+;
  Ff1; \1.- Leyendo : \; pzona_ind1,0; s1; psubzona_ind1,0; l;
  (zona_z)=(zona_ind1,#); (subzona_z)=(subzona_ind1,#);
  f4; k1; P;
  { n+;
    !(status_ind2)0($*,1)>|;
      !(ploc_z)0($C,1)>|;
        (contadosi_z)=(contadosi_z)(totfac_ind2)+;
      |
        (contadosi_z)=(contadosi_z)(prelis_ind2)+;
      .
    |
      !(ploc_z)0($C,1)>|;
        (contadono_z)=(contadono_z)(totfac_ind2)+;
      |
        (contadono_z)=(contadono_z)(prelis_ind2)+;
      .
    .
    f4; a;
  )
  f3; a;
)
(vtacon_z)=(contadosi_z)(contadono_z)+;
Ff1; \Ahora se genera el reporte ...\; l;
f3; k1; P; n|; z32,zona_ind1,#; . 
(zona_z)=(zona_ind1,#); (antmegazona_z)=(megazona_ind1,#);
Ff4; FO(arch_z),w~; GFONT34; FP66,60,Enc,Pie; f3; k1; P;
{ n+;
  Ff1; \Leyendo : \; pmegazona_ind1,0; s1; pzona_ind1,0; s1; 
  psubzona_ind1,0; l;
  !(megazona_ind1)5(antmegazona_z,#)>|; 
    GSUBTZON; GSUBT_MEGAZONA; 
    (zona_z)=(zona_ind1,#); (antmegazona_z)=(megazona_ind1,#); 
    !(saltop_z)0($S,1)>|; GSALTO_PAGINA; | GSUBENC_MEGAZONA; .
  .
  !(zona_ind1)5(zona_z,#)>|; 
    GSUBTZON; (zona_z)=(zona_ind1,#); GSUBENC; 
  .
  GIMPRI; f3; a;
)
GSUBTZON;
GSUBT_MEGAZONA;
!(saltop_z)0($S,1)>|; GSALTO_PAGINA; .
GULIN; Ff4; FC; Ff1; tf; tr; tS0; GSalir;
gFin;

[TOMA_PARAMETROS;
x echo 1 >>.vtasino.ini ~;
Ff5; FO.vtasino.ini~,r~;
(ii_z)=(0);
{ FG+; z0,milin_z,#; Fkmilin_z,l; 
  (ii_z)=(ii_z)(1)+;
  !(ii_z)0($1)>|; (ploc_z)=(milin_z,1); .
  !(ii_z)0($2)>|; (tasiva_z)=(milin_z,2); .
  !(ii_z)0($3)>|; (prmegazona_z)=(milin_z,2); .
  !(ii_z)0($4)>|; (ulmegazona_z)=(milin_z,2); .
  !(ii_z)0($5)>|; (saltop_z)=(milin_z,1); .
)
Ff5; FC; Ff1;
R;

[GRABA_PARAMETROS;
Ff5; FO.vtasino.ini~,w~;
pploc_z,0; l;
ptasiva_z,0; l;
pprmegazona_z,0; l;
pulmegazona_z,0; l;
psaltop_z,0; l;
Ff5; FC; Ff1;
R;

[SALTO_PAGINA;
!(saltop_z)0($S,1)>|; Ff4; GFONT30; .
R;

[Enc;
Ff3; FT; FGNodatos; Ff4; GFONT12; Fc80,3; l; Ff3; FGNodatos; Ff4;
GFONT13; \vtasino \; GFONT14; &a; &cfech8_z; &p3fech9_z; pfech8_z,+8;
s1; phora_z,#; s1; FW3; l;
GFONT15; Fc40,\INFORME DIRECCION~; GFONT16; l;
GFONT15; s4; \VENTAS DEL \; pfecha_z,+8; \ AL \; pfechb_z,+8; GFONT16; l;
GFONT15; Fc40,\INFORME DE VENTAS~; GFONT16; l;
GFONT17;
!(tasiva_z)0($XX,1)>|;
  \ACUMULADOS DE VENTAS SIN IMPORTAR TASA DE IVA \;
|
  \ACUMULADOS DE VENTAS IVA AL \; ptasiva_z,2;
.
l;
GFONT15; Fc30,\ACUMULADOS EN PRECIO ~;
!(ploc_z)0($L,1)>|; \ LISTA \; | \CREDITO\; . GFONT16; l;
\VENTAS DE MEGAZONA:\; pprmegazona_z,0; s1; 
(megazona_z)=(prmegazona_z,#); 
z0,nombre_ind3,l; f5; k3; P; n|;. pnombre_ind3,0;
!(prmegazona_z)5(ulmegazona_z,#)>|;
  \ A Megazona:\; pulmegazona_z,0; s1;
  (megazona_z)=(ulmegazona_z,#); 
  z0,nombre_ind3,l; f5; k3; P; n|;. pnombre_ind3,0;
.
l;
GFONT17; Fc80,\TOTAL FACTURACION TRADICIONAL~; GFONT18; l; l;
s4; \                T.Facturacion. ----- * ------    %     -------------    %   \; l;
GSUBENC_MEGAZONA;
Ft;

[Pie; Fe;

[SUBENC_MEGAZONA;
(megazona_z)=(antmegazona_z,#); 
z0,nombre_ind3,l; f5; k3; P; n|;. 
s4; GFONT17; \Megazona:\; pmegazona_z,0; s1; pnombre_ind3,0; GFONT18; l;
GSUBENC;
R;

[SUBENC;
f1; k1; P; n|; GNOZONA; .
Ff4; s4; GFONT17; pzona_z,0; s1; pnombre_zon,0; GFONT18; l;
R;

[IMPRI;
z0,vtacont_z,#; z0,vtaconsi_z,#; z0,vtaconno_z,#;
(subzona_z)=(subzona_ind1,#);
(zona_z)=(zona_ind1,#); (subzona_z)=(subzona_ind1,#);
f4; k1; P;
{ n+;
  !(status_ind2)0($*,1)>|;
    !(ploc_z)0($C,1)>|;
      (vtaconsi_z)=(vtaconsi_z)(totfac_ind2)+;
    |
     (vtaconsi_z)=(vtaconsi_z)(prelis_ind2)+;
    .
  |
    !(ploc_z)0($C,1)>|;
      (vtaconno_z)=(vtaconno_z)(totfac_ind2)+;
    |
      (vtaconno_z)=(vtaconno_z)(prelis_ind2)+;
    .
  .
  f4; a;
)
(vtacont_z)=(vtaconsi_z)(vtaconno_z)+;
f5; k1; P; n|;.
z0,porc1_z,#; z0,porc2_z,#;
!(vtacont_z)5($0)>|;
  (porc1_z)=(vtaconsi_z)(vtacont_z)/(100)*;
  (porc2_z)=(vtaconno_z)(vtacont_z)/(100)*;
.
Ff4; s4; pnombre_ind3,15; s1; pvtacont_z,MDS14; s1; 
pvtaconsi_z,MDS14; s1; pporc1_z,MDSF; `37; s1; 
pvtaconno_z,MDS14; s1; pporc2_z,MDSF; `37; l;
(vtaconz_z)=(vtaconz_z)(vtacont_z)+;
(contadosiz_z)=(contadosiz_z)(vtaconsi_z)+;
(contadonoz_z)=(contadonoz_z)(vtaconno_z)+;
R;

[SUBTZON;
z0,porc1_z,#; z0,porc2_z,#;
!(vtaconz_z)5($0)>|; 
  (porc1_z)=(contadosiz_z)(vtaconz_z)/(100)*;
  (porc2_z)=(contadonoz_z)(vtaconz_z)/(100)*;
.
Ff4; 
s5; \Total de Zona \;
c-14; s1; c-14; s1; c-7; s1; c-14; s1; c-7; l;
s4; s16; 
pvtaconz_z,MDS14; s1; 
pcontadosiz_z,MDS14; s1; 
pporc1_z,MDSF; `37; s1; 
pcontadonoz_z,MDS14; s1; 
pporc2_z,MDSF; `37; 
l;
z0,porc1_z,#; z0,porc2_z,#; z0,porc3_z,#;
!(vtacon_z)5($0)>|; (porc1_z)=(vtaconz_z)(vtacon_z)/(100)*; .
!(contadosi_z)5($0)>|; (porc2_z)=(contadosiz_z)(contadosi_z)/(100)*; .
!(contadono_z)5($0)>|; (porc3_z)=(contadonoz_z)(contadono_z)/(100)*; .
s4; \Porcen. S/Total \; s3; pporc1_z,MDSF; `37; s4; s1; s3; pporc2_z,MDSF; `37; s4;
s1; s7; s3; pporc3_z,MDSF; `37; l; l;
(contadonomzn_z)=(contadonomzn_z)(contadonoz_z)+; z0,contadonoz_z,#; 
(contadosimzn_z)=(contadosimzn_z)(contadosiz_z)+; z0,contadosiz_z,#; 
(vtaconmzn_z)=(vtaconmzn_z)(vtaconz_z)+;          z0,vtaconz_z,#;
R;

[SUBT_MEGAZONA;
z0,porc1_z,#; z0,porc2_z,#;
!(vtaconmzn_z)5($0)>|; 
  (porc1_z)=(contadosimzn_z)(vtaconmzn_z)/(100)*;
  (porc2_z)=(contadonomzn_z)(vtaconmzn_z)/(100)*;
.
Ff4; 
\Total de Mega Zona \;
c-14; s1; c-14; s1; c-7; s1; c-14; s1; c-7; l;
s4; s16; 
pvtaconmzn_z,MDS14; s1; 
pcontadosimzn_z,MDS14; s1; 
pporc1_z,MDSF; `37; s1; 
pcontadonomzn_z,MDS14; s1; 
pporc2_z,MDSF; `37; 
l;
z0,porc1_z,#; z0,porc2_z,#; z0,porc3_z,#;
!(vtacon_z)5($0)>|; (porc1_z)=(vtaconmzn_z)(vtacon_z)/(100)*; .
!(contadosi_z)5($0)>|; (porc2_z)=(contadosimzn_z)(contadosi_z)/(100)*; .
!(contadono_z)5($0)>|; (porc3_z)=(contadonomzn_z)(contadono_z)/(100)*; .
s4; \Porcen. S/Total \; s3; pporc1_z,MDSF; `37; s4; s1; s3; pporc2_z,MDSF; `37; s4;
s1; s7; s3; pporc3_z,MDSF; `37; l; l;
z0,contadonomzn_z,#; z0,contadosimzn_z,#; z0,vtaconmzn_z,#;
R;

[ULIN;
(ult_z)=(1); GSACA_TOTALES;
qD,2,fecacuini_z,fechmai_z,; qD,2,fecacufin_z,fechmaf_z,;
GACUMULA_TOTALES; (ult_z)=(2); GSACA_TOTALES;
qD,2,fecacuini_z,fechaai_z,; qD,2,fecacufin_z,fechaaf_z,;
GACUMULA_TOTALES; (ult_z)=(3); GSACA_TOTALES;
R;

[SACA_TOTALES;
z0,porc1_z,#; z0,porc2_z,#;
!(vtacon_z)5($0)>|;
  (porc1_z)=(contadosi_z)(vtacon_z)/(100)*; 
  (porc2_z)=(contadono_z)(vtacon_z)/(100)*;
.
Ff4; s4; GFONT17; 
!(ult_z)0($1)>|; \  GRAN TOTAL   \; .
!(ult_z)5($1)>|; 
  pfecacuinimm_z,MIS2; \/\; pfecacuiniaa_z,MIS2; \-\;
  pfecacufinmm_z,MIS2; \/\; pfecacufinaa_z,MIS2;
.
s1; pvtacon_z,MDS14; s1; 
pcontadosi_z,MDS14; s1; pporc1_z,MDSF; `37; s1;
pcontadono_z,MDS14; s1; pporc2_z,MDSF; `37; GFONT18; l;
R;

[ACUMULA_TOTALES;
z0,contadosi_z,#; z0,contadono_z,#;
f4; k3; P;
{ n+;
  *
  Ff1; pfecacuiniaa_z,MIS2; \-\; pfecacuiniaa_z,MIS2; \-\; pfecacuinidd_z,MIS2; 
  \:\; pfecacufinaa_z,MIS2; \-\; pfecacufinmm_z,MIS2; \-\; pfecacufindd_z,MIS2; 
  \:\; pstatus_ind2,0; \:\; ptotfac_ind2,MDS14; \:\; pprelis_ind2,MDS14; l; y|;.
  *
  !(status_ind2)0($*,1)>|;
    !(ploc_z)0($C,1)>|;
      (contadosi_z)=(contadosi_z)(totfac_ind2)+;
    |
      (contadosi_z)=(contadosi_z)(prelis_ind2)+;
    .
  |
    !(ploc_z)0($C,1)>|;
      (contadono_z)=(contadono_z)(totfac_ind2)+;
    |
      (contadono_z)=(contadono_z)(prelis_ind2)+;
    .
  .
  f4; a;
)
(vtacon_z)=(contadosi_z)(contadono_z)+;
R;


[MANDAELFNT; f13; k1; P; nR; GSACAF; R;

#isacaf.i;
#iimpri.i;

[ABRE_TABLA; 
Ff5; FObasedato.ini~,r~; FGNOBD; Fkmibd_z,l; FC; Ff1; 
fd1; O@,mibd_z,"sa","lux",1,ERRCNB,;  * con conexion autocommit    *
f1; #izonas.i;
k0; ot,"zonas",;
k1; kp,"zona=?",zona_z,#,1,;

f3; M
	megazona_ind1		s3,
	zona_ind1		s3,
	subzona_ind1		s4,
        prelis_ind1		d,
        totfac_ind1		d,;
k0; ot,"zontda",;
k1; kq,"select e.clave, c.zona, subzona, sum(precon), sum(totfact) 
		from acuvtas c
		join car_megazonasdet d on c.zona = d.zona
	join car_megazonas e on d.idmegazona = e.idmegazona
where voc='V' and fecini between ? and ? and e.clave between ? and ?
	group by e.clave, c.zona,subzona",
	fecini_z,#,3,fecult_z,#,3,prmegazona_z,#,1,ulmegazona_z,#,1,;

f4; M
	status_ind2		s2,
	prelis_ind2		d,
        totfac_ind2		d,
	tam_ind2		s0,;
k0; ot,"zontda",;
k1; kq,"select status, sum(precon), sum(totfact)
	from car_tipocar a 
	join car_tipocardet b on a.idtipocar = b.idtipocar
	join acuvtas c on b.ticte = c.ticte where
        tipocar = ? and voc=? 
	and fecini between ? and ? and zona=? and subzona=? 
        and piva between ? and ?
        and fecini = convert_date(
          convert_char(Year(fecfin))+ '-' + 
          convert_char(Month(fecfin)) + '-01'
        ) 
        and fecfin =  timestampadd(4, -1, timestampadd(6, 1, fecini))
	group by status",
	tipocar_z,#,1,voc_z,#,1,fecini_z,#,3,fecult_z,#,3,
        zona_z,#,1,subzona_z,#,1,piva1_z,piva2_z,;
k3; kq,"select status, sum(precon), sum(totfact)
	from car_tipocar a 
	join car_tipocardet b on a.idtipocar = b.idtipocar
	join acuvtas c on b.ticte = c.ticte where
        tipocar = ? and voc=?
	and fecini between ? and ? and piva between ? and ?
        and fecini = convert_date(
          convert_char(Year(fecfin))+ '-' + 
          convert_char(Month(fecfin)) + '-01'
        ) 
        and fecfin =  timestampadd(4, -1, timestampadd(6, 1, fecini))
        group by status",
	tipocar_z,#,1,voc_z,#,1,fecacuini_z,#,3,fecacufin_z,#,3,
        piva1_z,piva2_z,;

f5; M
	nombre_ind3		s31,
	tam_ind3		s0,;
k0; ot,"zontda",;
k1; kq,"select nombre from ubivta where zona=? and subzon=?",
	zona_z,#,1,subzona_z,#,1,;
k3; kq,"select descri from car_megazonas where clave=?",megazona_z,#,1,;

f6; M
	ultfec_ind5	{
          ultfecaa_ind5		i,
          ultfecmm_ind5		i,
          ultfecdd_ind5		i,
        		}
        tam_ind5		s0,;
k0; ot,"acuvtas",;
k1; kq,"select max(fecfin) from acuvtas",;

f13; #itablefnt.i;
k0; ot,"fonts",;
k1; kp,"impre=? and numero=?",impre_z,font_z,;
R;

#i/programas/lib/colores.i;
#i/programas/lib/tomamaxrc.i;
#i/programas/lib/status.i;
#i/programas/lib/fechas.i;

[PAN; 
Ff1; Gdefine_colores; Gtomamaxrc; Gamarillo_negro;
tf; tn; ta0; #iprog1.i; ^=direc_z,#); 
(modul_z)=($Informe Ventas Si/No,20); (modul2_z)=($CAR-MDS,7);
GBANNER; Gblanco_azul;
R;

[R; R; [E; ];
[NOZONA; z0,zona_zon,l; (nombre_zon)=($INEXISTENTE    ,15); R;
[NOBD; Ff1; GST; `7; \No existe el archivo basedato.ini ...\; gFin1;
[ERRCNB; GST; `6; \No pude conectarme a la base de datos...\; gFin1;
[Nodatos; Ff1; GST; `7; \archivo datos incompleto o inexistente...\; gFin1;

[Fin1; y|;.
[Fin; t@23,0; te; };
